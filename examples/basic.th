/**
* Basic program
*   Simple program showing various functionalities one usually expects
*   from a programming language
*/

// Create a variable
let foo = "Hello World";

// Semicolons are optional
let bar = 12

// Create a function
let my_function = fn(x) {
    // Call an inbuilt function
    print(x)
}
my_function(foo)

// Closures
let order = fn(quantity, item) {
    // String concatenation
    print("I would like " + quantity + " " + item)
}
let order_pies = fn(quantity) {
    order(quantity, "Pies")
}
order_pies(bar)

// If expressions
if bar == 12 {
    print("bar is 12")
} else {
    print("bar is not 12")
}

// If expressions can also be bound to a variable
let stress = 10
let qty = if stress > 5 {
    print("Cheat day")
    // You do not need an explicit return, it will use the last evaluated expression
    20
} else {
    1
}
order_pies(qty)

// Arrays
let arr = [1, 2 * 2, "Hello"];
print("")
print("--- Arrays ---")
print("Length of arr: " + len(arr))
// They are immutable and operations on them will return a new copy of the array
print(arr + 12)
print(arr + [10, 20])
print(tail(arr, 2))
print(first(arr))
print(last(arr))

// Loops
print("")
print("--- Loops ---")
let i = 0;
while i < 3 {
    i = i + 1
    print(i)
}
print("Value of i outside of loop: " + i)
// Loops are also expressions
let i = 0
let ret = while i < 10 {
    i = i + 1
}
print(ret)
// And can be returned early through the break keyword
let i = 0
let ret = while i < 10 {
    if i == 5 {
        break i
    }
    i = i + 1
}
print(ret)
